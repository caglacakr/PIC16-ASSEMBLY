;ÇARPIM TABLOSUNDAK? ??LEMLER? YAPAB?LEN HESAP MAK?NES?
    
    __CONFIG H'3F31'
    list p = 16f877a
    #include<p16f877.inc>
    
	;GECIKME DEGISKENLERI
	GECIKME1    EQU	0x20		;1.GECIKME ICIK
        GECIKME2    EQU	0x21		;2.GECIKME ICIN
	GECIKME3    EQU	0x22		;3.GECIKME ICIN
	
	;LCD DEGISKENLERI
	VERI4	      EQU	0X23		;VERININ EN DUSUK 4 BITI
	ISLEM	      EQU	0X24		;HANGI ISLEM KULLANILACAK?
	LCD_TEMP    EQU	0X25		;LCD TEMP DEGIS.
	LCD_TRIS    EQU	TRISB		;TRISB - TEMSILI
	LCD_PORT    EQU	PORTB		;PORTB - TEMSILI
	
	LCD_RS	    EQU	    4			;RS BITININ TEMSILI
	LCD_E	    EQU	    5			;ENABLE BITI TEMSIL
	    
	DEGER1	    EQU	    0X26		;ISLENECEK OPERAND-1
	DEGER2	    EQU	    0X27		;ISLENECEK OPERAND-2
	SIRA	    EQU	    0X28		;ISLEM SIRASI
	RESULT	    EQU	    0X29		;ISLEM SONUCU
	OTUZF	    EQU	    0X30		;RAKAMLARIN ASCII BASLANGICI
	    
	BIRLER	    EQU	    0X31
	ONLAR	    EQU	    0X32
	YEDEK	    EQU	    0X33  
	ON	    EQU	    0X34
	TMP	    EQU	    0X35
	    
	ORG 0X000
	NOP
	GOTO BASLA
	
	DUSEN_KENAR
	    BANKSEL	LCD_PORT		;BANK0 SEC.
	    BSF		LCD_PORT,LCD_E	;ONCE E UCUNU 1 YAP
	    CALL	GECIKME_200USN	;GECIKMEYE GIT
	    BCF		LCD_PORT,LCD_E	;E UCUNU 0 YAP KI UYAR
	    RETURN
	    
	KOMUT4_GONDER
	    ANDLW	0x0F			;WREG 'TEN GELEN VERININ EN DUSUK 4 BITINI AL.
	    MOVWF	VERI4			;BU 4 BITLIK PAKETI VERI DEGISKENINE AT.

	    MOVFW	LCD_PORT		;PORTB 'DEKI DEGERI WREG 'E YAZ. 
	    ANDLW	0xF0			;PORTB 'DEKI EN DEGERLI 4BITI AL (AYAR BITLERI)
	
	    IORWF	VERI4,W		;WREG LSB4 + PORTB MSB4 IKI PAKETI BIRLESTIR	
	    MOVWF	LCD_PORT		;BIRLESTIRDIGIN PAKETLERI PORTB 'YE ATA.
	
	    CALL	DUSEN_KENAR		;BU ISLEMLER ICIN ENABLE BITINI DURTMEN GEREKIR
	    CALL	GECIKME_200USN	;100MIKROSANIYE GECIKME
	    RETURN
	    
	KOMUT_GONDER
	    ;2 ASAMADA GONDERME YAPMAK - ICIN BU METOD KULLANILIR.
	
	    MOVWF	LCD_TEMP		; ONCE GONDERILECEK VERIYI SAKLA
	    SWAPF	LCD_TEMP,W		; GONDERILECEK VERININ ILK DOR VE SON DORT BITINI YER DEGISTIR

	    BANKSEL	LCD_PORT		; BANK0 'A GEC
	    BCF		LCD_PORT,LCD_RS	; RS UCUNU 0 A CEK VE KOMUT GONDERECEGINI SOYLE

	    CALL	KOMUT4_GONDER	; BU EN DEGERLIKLI 4 BITI YOLLA

	    MOVF	LCD_TEMP,W		; SIMDI EN DEGERLIKSIZ 4 U GONDER
	    BCF		LCD_PORT,LCD_RS	; RS UCUNU 0 A CEK VE KOMUT GONDERECEGINI SOYLE
	    CALL	KOMUT4_GONDER
	
	    RETURN
	    
	KARAKTER_GONDER
	    MOVWF	LCD_TEMP		; GELEN VERI ICIN SWAP ISLEMI YAPILACAGINDAN KORU
	    SWAPF	LCD_TEMP,W		; EN DEGERLIKLI BITLER ICIN SWAP YAP
	    BANKSEL	LCD_PORT		; BANK0 A GECIYORUZ.
	    BSF		LCD_PORT,LCD_RS	; KARAKTER YAZMA ICIN RS=1
	    CALL	KOMUT4_GONDER	; 4 BIT MODUNDA KARAKTERI GONDER
	    MOVF	LCD_TEMP,W		; SIMDI NORMAL DUSUK DEGERLIKLI BITLERI GONDER
	    CALL	KOMUT4_GONDER
	    RETURN

	LCD_AYARLA
	    MOVLW 0X00
	    MOVWF DEGER1			;OPERAND BIR HENUZ SIFIR.
	    MOVWF DEGER2			;OPERAND IKI HENUZ SIFIR.
	    MOVWF BIRLER
	    MOVWF ONLAR
	    
	    MOVLW 0X30
	    MOVWF OTUZF
	    
	    MOVLW D'10'
	    MOVWF ON
	    
	    MOVLW 0X05
	    MOVWF SIRA			;ILK BASTA RAKAM OKUNMALI
	    
	    
	    BANKSEL	LCD_TRIS		;BANK1 SECIYORUZ.
	    CLRF	LCD_TRIS		;B PORTUNU TAMAMEN CIKIS YAP.

	    BANKSEL	LCD_PORT		;BANK0 SECIYORUZ.
	    CALL	GECIKME_50MSN	;ACILIS ICIN 50MS BEKLE.
	    BCF		LCD_PORT,LCD_RS	;RS BITINI 0 YAP (KOMUT)	

	    	    
	    ;GONDERECEGIMIZ KOMUTLAR
	    MOVLW	0x03			;0X03 GONDERMEMIZ GEREK.
	    CALL	KOMUT_GONDER		;KOMUTU GONDERIYORUZ
	    CALL	GECIKME_5MSN		;5MS BEKLIYORUZ.
	    
	    MOVLW	0x02			;0X02 GONDERMEMIZ GEREK.
	    CALL	KOMUT_GONDER		;KOMUTU GONDERIYORUZ
	    CALL	GECIKME_200USN	;BIRAZ BEKLIYORUZ.
	    
	    MOVLW	0x28
	    CALL	KOMUT_GONDER

	    MOVLW	0x10	
	    CALL	KOMUT_GONDER

	    MOVLW	0x01
	    CALL	KOMUT_GONDER

	    MOVLW	0x06
	    CALL	KOMUT_GONDER

	    MOVLW	0x0D
	    CALL	KOMUT_GONDER
	   
	    
	KEYPAD_AYARLA
	    BANKSEL TRISD
	    MOVLW 0X0F
	    MOVWF TRISD
	    BANKSEL PORTA
	    RETURN
	    
	    
	CLEAR_LCD
	    MOVLW 0X01
	    CALL KOMUT_GONDER
	    
	    MOVLW 0X00 
	    MOVWF DEGER1
	    MOVWF DEGER2
	    MOVWF BIRLER
	    MOVWF ONLAR
	    MOVWF ISLEM
	    
	    MOVLW 0X05
	    MOVWF SIRA   
	    GOTO  TARA_RAKAM
	    
	SONUC
	    MOVLW 0X2B
	    SUBWF ISLEM,W
	    BTFSC STATUS,Z
	    GOTO  TOPLA_F
	    
	    MOVLW 0X2D
	    SUBWF ISLEM,W
	    BTFSC STATUS,Z
	    GOTO  CIKAR_F
	    
	    MOVLW 0X2F
	    SUBWF ISLEM,W
	    BTFSC STATUS,Z
	    GOTO  BOL_F
	    
	    MOVLW 0X78
	    SUBWF ISLEM,W
	    BTFSC STATUS,Z
	    GOTO  CARP_F

	    GOTO SONUC
	
	TOPLA_F
	    MOVFW DEGER1
	    ADDWF DEGER2,W
	    MOVWF RESULT
	    GOTO  SONUC_DENETLE
	    
	    
	EKSI
	    MOVFW DEGER2
	    MOVWF TMP	    ;DEGER2 VAR
	    MOVFW DEGER1
	    MOVWF DEGER2
	    MOVFW TMP
	    MOVWF DEGER1

	    MOVFW DEGER2
	    SUBWF DEGER1,W
	    MOVWF RESULT
	    GOTO  SONUC_DENETLE
	    
	CIKAR_F
	    MOVFW DEGER2
	    BSF STATUS,C
	    SUBWF DEGER1,W
	    BTFSS STATUS,C
	    GOTO EKSI
	    
	
	    MOVFW DEGER2
	    SUBWF DEGER1,W
	    MOVWF RESULT
	    GOTO  SONUC_DENETLE
	    
	BOL_F
	    CLRF RESULT
	    MOVFW DEGER2
	D_BOL
	    MOVFW DEGER2
	    BSF STATUS,C
	    SUBWF DEGER1,F
	    BTFSS STATUS,C
	    GOTO SONUC_DENETLE 
	    INCF RESULT,F
	    GOTO D_BOL
	    
	    
	CARP_F
	    CLRW
	D_CARP
	    ADDWF  DEGER1,W
	    DECFSZ DEGER2,F
	    GOTO D_CARP
	    MOVWF RESULT
	    GOTO SONUC_DENETLE
	    
	SONUC_DENETLE
	    MOVFW ON
	    BSF STATUS,C
	    SUBWF RESULT,W
	    BTFSS STATUS,C
	    GOTO  BIR_BASAMAK
	    GOTO  IKI_BASAMAK
	    
	BIR_BASAMAK
	    MOVFW OTUZF
	    ADDWF RESULT,W
	    CALL  KARAKTER_GONDER
	    GOTO  CALIS
	
        IKI_BASAMAK
	    MOVFW ON
	    BSF STATUS,C
	    SUBWF RESULT,F
	    BTFSS STATUS,C
	    GOTO IKI_YAZ_BOLUM
	    INCF ONLAR,F
	    GOTO IKI_BASAMAK
		
	IKI_YAZ_BOLUM
	    MOVFW ON
	    ADDWF RESULT,F
	    MOVFW RESULT
	    MOVWF BIRLER
	    
	IKI_YAZ
	    MOVFW   OTUZF
	    ADDWF   ONLAR,W
	    CALL    KARAKTER_GONDER
	    MOVFW   OTUZF
	    ADDWF   BIRLER,W
	    CALL    KARAKTER_GONDER
	    
	    GOTO CALIS
	    
	DEGERLENDIR
	    MOVWF YEDEK
	    MOVLW 0X04
	    SUBWF SIRA,W
	    BTFSC STATUS,Z
	    CALL  BIRE_ATA		;DEGISKEN1 'E ATAMA YAP
	    MOVLW 0X02
	    SUBWF SIRA,W		
	    BTFSC STATUS,Z
	    GOTO IKIYE_ATA		;DEGISKEN2 'YE ATAMALAR
	    
	BIRE_ATA
	    MOVFW YEDEK
	    MOVWF DEGER1
	    RETURN
	    
	IKIYE_ATA   
	    MOVFW YEDEK
	    MOVWF DEGER2
	    RETURN
	
	DOKUZ
	    MOVLW '9'
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW   D'9'
	    CALL    DEGERLENDIR
	    RETURN
	  
	SEKIZ
	    MOVLW '8'
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW   D'8'
	    CALL    DEGERLENDIR
	    RETURN
	
	YEDI
	    MOVLW '7'
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW   D'7'
	    CALL    DEGERLENDIR
	    RETURN
	
	ALTI
	    MOVLW '6'
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW   D'6'
	    CALL    DEGERLENDIR
	    RETURN
	 
	BES
	    MOVLW '5'
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW   D'5'
	    CALL    DEGERLENDIR
	    RETURN

	DORT
	    MOVLW '4'
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW   D'4'
	    CALL    DEGERLENDIR
	    RETURN    
	    
	UC
	    MOVLW '3'
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW   D'3'
	    CALL    DEGERLENDIR
	    RETURN
	    
	IKI 
	    MOVLW '2'
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW   D'2'
	    CALL    DEGERLENDIR
	    RETURN
	    
	BIR
	    MOVLW '1'
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW   D'1'
	    CALL    DEGERLENDIR
	    RETURN
	    
	SIFIR
	    MOVLW '0'
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW   D'0'
	    CALL    DEGERLENDIR
	    RETURN
	   
	ESIT	
	    MOVLW 0X3D
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    RETURN
	    
	BOLME
	    MOVLW 0X2F
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW 0X2F
	    MOVWF ISLEM
	    RETURN
	    
	CARPMA
	    MOVLW 0X78
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW 0X78
	    MOVWF ISLEM
	    RETURN
	    
	TOPLAMA
	    MOVLW 0X2B
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW 0X2B
	    MOVWF ISLEM
	    RETURN
	    
	CIKARMA
	    MOVLW 0X2D
	    CALL KARAKTER_GONDER
	    DECF SIRA,F
	    
	    MOVLW 0X2D
	    MOVWF ISLEM
	    RETURN
	   
	BASLA
	    CALL	LCD_AYARLA
	    CALL	KEYPAD_AYARLA
	    
	TARA_RAKAM	
	    MOVLW 0X04
	    SUBWF SIRA,W
	    BTFSC STATUS,Z
	    GOTO TARA_ISLEM
	    MOVLW 0X02
	    SUBWF SIRA,W 
	    BTFSC STATUS,Z
	    GOTO TARA_ESITTIR
	    
	    SUTUN_963
	    MOVLW b'01000000'
	    MOVWF PORTD			;963 SUTUNU AKTIF
	    
	    BTFSC   PORTD,0			;9 MU?	
	    CALL    DOKUZ
	    BTFSC   PORTD,1			;6 MI?
	    CALL    ALTI
	    BTFSC   PORTD,2			;3 MU?
	    CALL    UC	
	    
	    SUTUN_852
	    MOVLW b'00100000'
	    MOVWF PORTD			;8520 SUTUNU AKTIF
	    
	    BTFSC   PORTD,0			;8 MI?	
	    CALL    SEKIZ
	    BTFSC   PORTD,1			;5 MI?
	    CALL    BES
	    BTFSC   PORTD,2			;2 MI?
	    CALL    IKI	
	    BTFSC   PORTD,3			;0 MI?
	    CALL    SIFIR

	    SUTUN_741
	    MOVLW b'00010000'
	    MOVWF PORTD			;741  SUTUNU AKTIF
	    
	    BTFSC   PORTD,0			;7 MI?	
	    CALL    YEDI
	    BTFSC   PORTD,1			;4 MU?
	    CALL    DORT
	    BTFSC   PORTD,2			;1 MI?
	    CALL    BIR	
	    BTFSC   PORTD,3			;CLEAR MI?
	    GOTO    CLEAR_LCD
	    
	    
	    CALL GECIKME_KEYPAD
	    GOTO TARA_RAKAM
	    
	TARA_ISLEM
	    MOVLW 0X03
	    SUBWF SIRA,W
	    BTFSC STATUS,Z
	    GOTO  TARA_RAKAM
	
	    MOVLW b'10000000'
	    MOVWF PORTD
	    
	    BTFSC   PORTD,0			;/ MU?	
	    CALL    BOLME
	    BTFSC   PORTD,1			;X MI?
	    CALL    CARPMA
	    BTFSC   PORTD,2			;- MI?
	    CALL    CIKARMA
	    BTFSC   PORTD,3			;+ MI?
	    CALL    TOPLAMA
	    
	    MOVLW   b'00010000'		;CLEAR MI?
	    MOVWF   PORTD
	    BTFSC   PORTD,3			
	    GOTO    CLEAR_LCD
 
	    CALL GECIKME_KEYPAD
	    GOTO TARA_ISLEM
	
	TARA_ESITTIR
	    MOVLW 0X01
	    SUBWF SIRA,W
	    BTFSC STATUS,Z
	    GOTO  SONUC
	
	    MOVLW b'01000000'
	    MOVWF PORTD			;963 SUTUNU AKTIF
		
	    BTFSC   PORTD,3
	    CALL    ESIT
		
	    MOVLW   b'00010000'		;CLEAR MI?
	    MOVWF   PORTD
	    BTFSC   PORTD,3			
	    CALL    CLEAR_LCD
		
	    CALL GECIKME_KEYPAD
	    GOTO TARA_ESITTIR

	CALIS
	    MOVLW   b'00010000'		;CLEAR MI?
	    MOVWF   PORTD
	    BTFSC   PORTD,3			
	    GOTO    CLEAR_LCD
	    NOP
	    GOTO CALIS    
	 
	    
;****	GECIKMELERIN TAMAMI ***************************************
	    
	GECIKME_200USN				
	    MOVLW	0xC8
	    MOVWF	GECIKME1
	DNGU1
	    DECFSZ	GECIKME1,1
	    GOTO	DNGU1
	    RETURN
	
	GECIKME_5MSN
	    MOVLW	0xE7
	    MOVWF	GECIKME1
	    MOVLW	0x04
	    MOVWF	GECIKME2
	D5_D
	    DECFSZ	GECIKME1, f
	    GOTO	D5_D
	    DECFSZ	GECIKME2, f
	    GOTO	D5_D
	    RETURN
	
	GECIKME_50MSN
	    MOVLW	0x0F
	    MOVWF	GECIKME1
	    MOVLW	0x28
	    MOVWF	GECIKME2
	D50_D
	    DECFSZ	GECIKME1, f
	    GOTO	D50_D
	    DECFSZ	GECIKME2, f
	    GOTO	D50_D
	    RETURN
	    
	GECIKME_KEYPAD
	    MOVLW	0xFE
	    MOVWF	GECIKME1
	    MOVLW	0xFF
	    MOVWF	GECIKME2
	DK_D
	    DECFSZ	GECIKME1, f
	    GOTO	DK_D
	    DECFSZ	GECIKME2, f
	    GOTO	DK_D
	    RETURN

	BITIR
END
	


