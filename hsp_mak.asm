
	;KODLAMA SABLONU

	list		p=16f877A	; hangi pic
	#include	<p16f877A.inc>	; SFR register 'lar?n tan?mland??? kutuphane
	
	__CONFIG H'3F31'

; WDT, ossilatör gibi register ayarlar?

	
;KULLANILACAK DEGISKENLER

GECIKME1	EQU	0x20    ;GEC?KME 1. DONGU
GECIKME2	EQU	0x21    ;GECIKME 2. DONGU
GECIKME3	EQU	0x22    ;GECIKME 3.DONGU

DUSUK_4		EQU	0x23	;4 BITLIK KOMUT GONDERIMINDE KULLANILACAK
VERI		EQU	0x24	; 8 BITLIK LCD VERISI TUTULACAK
SATIR_KONUM	EQU	0x25	; LCD EKRANIN GIDILECEK SATIR BILGISI
SUTUN_KONUM	EQU	0x26	; LCD EKRANIN GIDILECEK SUTUN BILGISI

BCD1		EQU	0x27	; ILK DIGIT DECIMAL ICIN
BCD2		EQU	0x28	; IKINCI DIGIT
BCD3		EQU	0x29	; UCUNCU DIGIT
BCD4		EQU	0x2A	; DORDUNCU DIGIT
BCD5		EQU	0x2B	; BESINCI DIGIT
SAYI_L		EQU	0x2C	; 16 BITLIK SAYININ LOW 8 BITI
SAYI_H		EQU	0x2D	; 16 BITLIK SAYININ HIGH 8 BITI
S_TMP		EQU	0x2F	; GERIYE SAYIMDA GECICI DEGISKEN, L ICIN
S_TMP2		EQU	0x30	; GERIYE SAYIMDA KULLANILACAK, H ICIN
SAYI1		EQU	0x31	; ILK SAYI
SAYI2		EQU	0x32	; IKINCI SAYI
EKSI		EQU	0x33	; SONUC EKSI ISE "-" ISERETI CIKARMAK ICIN KONTROL REGISTERI. IKINCI BIT AYNI ISARETLERI TOPLAMAK ICIN KULLANILACAK
SAYI1_IS	EQU	0x34	; BIRINCI SAYI ISARETI (ISTENSE HER SAYI ISARETI BIR REG DE KONTROL EDILEBILIR)
SAYI2_IS	EQU	0x35	; IKINCI SAYI ISARETI
TMP		EQU	0x36	; GECICI ISLER ICIN
HANGI_ISL	EQU	0x37	; 1=TOPLAMA-CIKARMA, 2=CARPMA, 4=BOLME
S1_1		EQU	0x38	; BIRINCI SAYININ BIRINCI DIGITI (LCD YE GIRILEN RAKAMLARI BINARY TIPINE DONUSTURMEK ICIN)
S1_2		EQU	0x39	; BIRINCI SAYININ IKINCI DIGITI
S1_3		EQU	0x3A	; BIRINCI SAYININ UCUNCU DIGITI
S2_1		EQU	0x3B	; IKINCI SAYININ BIRINCI DIGITI 
S2_2		EQU	0x3C	; IKINCI SAYININ IKINCI DIGITI
S2_3		EQU	0x3D	; IKINCI SAYININ UCUNCU DIGITI
HANGI_SAYI	EQU	0x3E	; KEY DEN GIRILEN SAYI, BIRINCI SAYIMI (.1) YOKSA IKINCI SAYIMI (.2)
		


;***** Kesme durumunda kaydedilmesi gereken SFR ler icin kullanilacak yardimci degiskenler
w_temp		EQU	0x7D		
status_temp	EQU	0x7E		
pclath_temp	EQU	0x7F					


;********************************************************************************************
	ORG     0x000             	; Baslama vektoru

	nop			  			  	; ICD ozelliginin aktif edilmesi icin gereken bekleme 
  	goto    BASLA              	; baslama etiketine gir

	
;**********************************************************************************************
	ORG     0x004             	; kesme vektoru

	movwf   w_temp            	; W n?n yedegini al
	movf	STATUS,w          	; Status un yedegini almak icin onu once W ya al
	movwf	status_temp       	; Status u yedek register '?na al
	movf	PCLATH,w	  		; PCLATH '? yedeklemek icin onu once W 'ya al
	movwf	pclath_temp	  		; PCLATH '? yedek register a al

	; gerekli kodlar

	movf	pclath_temp,w	  	; Geri donmeden once tum yedekleri geri yukle
	movwf	PCLATH		  		
	movf    status_temp,w     	
	movwf	STATUS            	
	swapf   w_temp,f
	swapf   w_temp,w          	
	retfie                    	; Kesme 'den don
;***********************************************************************************************

;************************** TMR SIZ 5sn GECIKME ***********************************************

GECIKME_200USN				
	MOVLW	0xC8
	MOVWF	GECIKME1
DNGU1
	DECFSZ	GECIKME1,1
	GOTO	DNGU1
	RETURN
;****************************************************************************************************************

	
GECIKME_5MSN
	movlw	0xE7
	movwf	GECIKME1
	movlw	0x04
	movwf	GECIKME2
D5_D
	decfsz	GECIKME1, f
	goto	D5_D
	decfsz	GECIKME2, f
	goto	D5_D
	RETURN
;******************************************************************************************************
	
GECIKME_50MSN
	movlw	0x0F
	movwf	GECIKME1
	movlw	0x28
	movwf	GECIKME2
D50_D
	decfsz	GECIKME1, f
	goto	D50_D
	decfsz	GECIKME2, f
	goto	D50_D
	RETURN
;*****************************************************************************************************
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;BCD KOD COZME;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
BCD_KODLA
	MOVF	SAYI_H,0
	MOVWF	S_TMP2
	BTFSC	STATUS,Z	    ; EGER H SAYISI 0 ISE SADECE L SAYISINA BAK
	GOTO	DL
	CALL	H_BITIR		    ; ONCE H REGISTER I BITIR
	
DL	MOVF	SAYI_L,0	    ; H SAYMASI BTINCE L 'I USTUNE SAY
	BTFSC	STATUS,Z	    ; YUKLENEN SAYI 0 ISE GERIYE DON
	RETURN
	MOVWF	S_TMP		    ; GERI SAYILACAK SAYIYI GECICI REG E AL
D1	CALL	BCD_KONTROL
	DECF	S_TMP,1		    ; SAYI1 I TEKER TEKER AZALTMAYA BASLA VE SIFIR OLUNCA BITIR
	BTFSC	STATUS,Z	    ; SAYI 0 A ULASTIYSA HERSEY TAMAM
	RETURN
	GOTO	D1

;******************************************************************************************************
	
;******************************************************************************************************

BCD_KONTROL
	INCF	BCD1,1
	MOVLW	d'10'
	SUBWF	BCD1,0
	BTFSS	STATUS,Z
	RETURN
	BCF	STATUS,Z	;ASLINDA GEREK YOK!
	CLRF	BCD1
	INCF	BCD2,1
	MOVLW	d'10'
	SUBWF	BCD2,0
	BTFSS	STATUS,Z
	RETURN
	CLRF	BCD2
	INCF	BCD3,1
	MOVLW	d'10'
	SUBWF	BCD3,0
	BTFSS	STATUS,Z
	RETURN
	CLRF	BCD3
	INCF	BCD4,1
	MOVLW	d'10'
	SUBWF	BCD4,0
	BTFSS	STATUS,Z
	RETURN
	CLRF	BCD4
	INCF	BCD5,1
	MOVLW	d'10'
	SUBWF	BCD5,0
	BTFSS	STATUS,Z
	RETURN
	RETURN

;*******************************************************************************************************
	
H_BITIR
	MOVLW	0x00		; H KADAR 255 E KADAR SAY
	MOVWF	S_TMP		; L 'A BASLAYANA KADAR S_TMP BOSTA OLDUGU ICIN KULLANABILIRIZ
TT	CALL	BCD_KONTROL
	DECFSZ	S_TMP,1		
	GOTO	TT
	DECFSZ	S_TMP2,1
	GOTO	H_BITIR
	RETURN

;********************************************************************************************************	

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; BCD KOD COZME BITTI ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	
;**************************************************************************
ASCII_LOOKUP
	ADDWF	PCL,1			    ;W DAK? DEGERI PROGRAM COUNTER PCL INE EKLE VE UYGUN DISPLAY CIKTISINI W YA AL
	RETLW	0x30
	RETLW	0x31
	RETLW	0x32
	RETLW	0x33
	RETLW	0x34
	RETLW	0x35
	RETLW	0x36
	RETLW	0x37
	RETLW	0x38
	RETLW	0x39
	
;****************************************************************************************************************

DUSEN_KENAR
	BANKSEL	PORTB
	BSF	PORTB,5		;E UCUNU ONCE 1 YAP
	CALL	GECIKME_200USN	;ASLINDA BU KADAR UZUN OLMAK ZORUNDA DEGIL
	BCF	PORTB,5		; E UCUNU TEKRAR 0 YAP
	RETURN

;***********************************************************************************************
KOMUT_4BIT
	; BU FONKSIYON 4 BITLIK KOMUT GONDERME ICIN KULLANILIR
	;BURADA DIKKAT EDILMESI GEREKEN PORTB NIN AYAR BITLERINI DE ICEREN 4-7 NOLU BITLERININ DURUMLARININ BOZULMAMASI
	;GONDERILECEK 4 BIT BU FONKSIYON CAGRILMADAN	W YA YUKLENMELI. BUNLARI BIT BIT GONDERMEKTENSE MEVCUT PORTB NIN
	;DEGERLI 4 BITI ILE GELEN W VERISININ 4 BITINI BIRLESTIR
	
	ANDLW	0x0F	; W 'DAKI DEGERIN SADECE ILK DORT BITINI AL SAKLA
	MOVWF	DUSUK_4
	
	MOVF	PORTB,W	; PORTB NIN EN DEGERLI 4 BITINI ALMAK ICIN W 'YA GETIR
	ANDLW	0xF0	; SADECE EN DEGERLI 4 BITI
	
	IORWF	DUSUK_4,W   ; ILK DORT BITI TUTAN DEGISKEN ILE PORTB NIN SON 4 UNU VEYA ISLEMI ILE BIRLESTIR
	
	MOVWF	PORTB	;BIRLESMIS VERIYI LCD YE GONDER. BOYLECE MEVCUT PORTB DURUMU KORUNMUS OLDU
	
	CALL	DUSEN_KENAR ; KOMUT GONDERMEK ICIN E UCUNAN DUSEN KENAR GONDER
	CALL	GECIKME_200USN
	RETURN
	
;******************************************************************************************************

KOMUT_8BIT
	; BU FONKSIYON 8 BITLIK KOMUT GONDERMEK ICIN KULLANILACAK. CALISMASINDA YUKARIDAKI KOMUT_4 FONKSIYONUNDAN
	; FAYDALANACAK. BU FONKSIYON CAGRILDIGINDA GONDERILECEK VERI W 'YA YUKLENMIS OLMALI
	; GONDERILECEK KOMUTUN ONCE EN DEGERLIKLI 4 BITI SONRA EN DEGERLIKSIZ 4 BITI GONDERILECEK
	; ONCE EN DEGERLI 4 BITINI GONDERMEK ICIN SWAPF KOMUTU ILE SON 4 BIT BASA GETIRILIR
	
	MOVWF	VERI	; ONCE GONDERILECEK VERIYI SAKLA
	SWAPF	VERI,W	; GONDERILECEK VERININ ILK DOR VE SON DORT BITINI YER DEGISTIR VE W YA AT
	
	BANKSEL	PORTB
	BCF	PORTB,4	; RS UCUNU 0 A CEK VE KOMUT GONDERECEGINI SOYLE
	
	CALL	KOMUT_4BIT  ; BU EN DEGERLIKLI 4 BITI YOLLA
	
	MOVF	VERI,W	    ; SIMDI EN DEGERLIKSIZ 4 U GONDER ( VERI DEGISKENI NORMAL DURUMU TUTUYORDU)
	BCF	PORTB,4	; RS UCUNU 0 A CEK VE KOMUT GONDERECEGINI SOYLE
	CALL	KOMUT_4BIT
	
	RETURN

;***************************************************************************************************
	
LCD_KARAKTER_YAZ
	MOVWF	VERI	; GELEN VERI ICIN SWAP ISLEMI YAPILACAGINDAN KORU
	SWAPF	VERI,W	    ; EN DEGERLIKLI BITLER ICIN SWAP YAP
	BANKSEL	PORTB
	BSF	PORTB,4	    ; KARAKTER YAZMA ICIN RS=1
	CALL	KOMUT_4BIT  ; 4 BIT MODUNDA KARAKTERI GONDER
	MOVF	VERI,W	    ; SIMDI NORMAL DUSUK DEGERLIKLI BITLERI GONDER
	CALL	KOMUT_4BIT
	RETURN
	
;****************************************************************************************************
LCD_AYARLA
	BANKSEL	TRISB
	CLRF	TRISB
	
	BANKSEL	PORTB
		
	CALL	GECIKME_50MSN	;LCD ILK ACILIS GECIKMESI
	
	
	BCF	PORTB,4		;RS UCUNU 0 YAPARAK LCD YE KOMUT GONDERECEGINI SOYLE
	
	MOVLW	0x03
	CALL	KOMUT_4BIT
	CALL	GECIKME_5MSN
	
	CALL	DUSEN_KENAR
	CALL	GECIKME_200USN
	CALL	DUSEN_KENAR
	CALL	GECIKME_200USN
	
	BCF	PORTB,4
	MOVLW	0x02
	CALL	KOMUT_4BIT
	CALL	GECIKME_200USN
	
	
	
	MOVLW	0x28
	CALL	KOMUT_8BIT
	
	MOVLW	0x10		; EKRAN AKTIF, IMLEC YAN SON
	CALL	KOMUT_8BIT
	
	MOVLW	0x01
	CALL	KOMUT_8BIT
	
	CALL	GECIKME_5MSN
	
	MOVLW	0x06
	CALL	KOMUT_8BIT
	
	MOVLW	0x0D
	CALL	KOMUT_8BIT
	
	RETURN
;*******************************************************************************************************
	
KURSOR_KONUM
	MOVLW	0x80		    ; EKRANIN ILK SATIR BIRINCI SUTUN NOKTA KOORDINATI 0x80 DIR
	MOVF	SATIR_KONUM,1	    ; KENDI UZERINE KOPYALA, EGER 0 ISE SIFIRINCI SATIRDA KAL
	BTFSS	STATUS,Z	    ; EGER DEGILSE IKINCI SATIR BASLANGIC ADRESI 0x80 DEN BASLA
	MOVLW	0xC0
	ADDWF	SUTUN_KONUM,W	    ; SATIR BELLI OLDU SUTUN SAYISINI EKLE VE KONUMA GIT
	CALL	KOMUT_8BIT
	RETURN

;*******************************************************************************************************
	

TOPLA
	MOVF	SAYI1,0
	BCF	STATUS,C    ; STATUS C KULLANILACAGINDAN ONU BASTA RESETLEYELIM
	ADDWF	SAYI2,0
	BTFSC	STATUS,C
	INCF	SAYI_H,1    ; ELDE VARSA DIGER REGISTERI 1 ARTTIR (8 BIT ICIN EN FAZLA 1FE OLUR)
	MOVWF	SAYI_L
	RETURN
;**********************************************************************
YER_DEGIS			; BU FONKSIYON CIKARMA ISLEMINDE POZITIF SAYIYI SAYI1 E TASIMAK ICIN
	MOVF	SAYI1,0
	MOVWF	TMP
	MOVF	SAYI2,0
	MOVWF	SAYI1
	MOVF	TMP,0
	MOVWF	SAYI2
	RETURN
;*****************************************************************************************
CIKAR
	BTFSC	SAYI1_IS,0  ; MAKSAT POZITIF SAYIYI W YA ALMAK. SAYI1 NEGATIF OLAN ISE SAYI1 VE 2 YI YER DEGIS
	CALL	YER_DEGIS
	MOVF	SAYI2,0
	BCF	STATUS,C
	SUBWF	SAYI1,0
	BTFSS	STATUS,C    ; CIKARMADA SONUC NEGATIF ISE C=0 OLUR UNUTMA!
	BSF	EKSI,0	    ; EKRANDA SONUCU EKSI GOSTER
	MOVWF	SAYI_L
	RETURN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

CARP
	BCF	STATUS,C
	MOVF	SAYI1,0	    ; SAYI1 I SAYI_L YA AL
	ADDWF	SAYI_L,1    ; HE SEFERINDE SAYI1 I SAYI_L E EKLE
	BTFSS	STATUS,C    ; SAYI L 255 I ASTI ISE SAYI_H I 1 ARTTIR
	RETURN
	INCF	SAYI_H,1
	RETURN

;******************************************************************************************************
TEMIZLE
	CLRF	EKSI
	CLRF	SAYI_L
	CLRF	SAYI_H
	
	CLRF	BCD1	;LCD DE RAKAMLARI TEKER TEKER GOSTERMEK ICIN BCD 
	CLRF	BCD2
	CLRF	BCD3
	CLRF	BCD4
	CLRF	BCD5
	RETURN
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
;LCD EKRAN SIL
	
EKRAN_SIL
	MOVLW	0x01
	CALL	KOMUT_8BIT
	BANKSEL	HANGI_SAYI
	CLRF	HANGI_SAYI
	BSF	HANGI_SAYI,0	; VARSAYILAN OLARAK GIRILEN ILK SAYI BIRINCI SAYI
	CLRF	S1_1
	CLRF	S1_2
	CLRF	S1_3
	CLRF	S2_1
	CLRF	S2_2
	CLRF	S2_3
	CLRF	SAYI1
	CLRF	SAYI2
	CLRF	HANGI_ISL
	CLRF	SAYI1_IS
	CLRF	SAYI2_IS
	CLRF	SAYI_H
	CLRF	SAYI_L
	CLRF	BCD1		    ; BCD KODA CEVIRMEDEN KULLANILACAK DEGISKENLERI SIFIRLA
	CLRF	BCD2
	CLRF	BCD3
	CLRF	BCD4
	CLRF	BCD5
	
	RETURN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
YERLESTIR
	
	BTFSC	HANGI_SAYI,0
	GOTO	BIRINCI_S
	BTFSC	HANGI_SAYI,1
	GOTO	IKINCI_S
	
YER_BIT
	RETURN
	
BIRINCI_S
	BTFSC	HANGI_SAYI,4	; BU REGISTERIN 2-3-4 NOLU BITLERINDEN HANGILERI 1 ISE O BASAMAGA SAYI GIRILMISTIR
	GOTO	BIR_2
	MOVF	TMP,0
	MOVWF	S1_1		; GIRILEN SAYIYI BIRINCI SAYININ BIRINCI DIGITINE AL
	BSF	HANGI_SAYI,4	; BU REG IN 2 NOLU BITI 1 YAPILDI YANI 1. DIGIT GIRILDI
	GOTO	YER_BIT
	
BIR_2
	BTFSC	HANGI_SAYI,3	; BU REGISTERIN 2-3-4 NOLU BITLERINDEN HANGILERI 1 ISE O BASAMAGA SAYI GIRILMISTIR
	GOTO	BIR_3
	MOVF	TMP,0
	MOVWF	S1_2		; GIRILEN SAYIYI BIRINCI SAYININ IKINCI DIGITINE AL
	BSF	HANGI_SAYI,3	; BU REG IN 3 NOLU BITI 1 YAPILDI YANI 2. DIGIT GIRILDI
	GOTO	YER_BIT	
BIR_3
	BTFSC	HANGI_SAYI,2	; BU REGISTERIN 2-3-4 NOLU BITLERINDEN HANGILERI 1 ISE O BASAMAGA SAYI GIRILMISTIR
	GOTO	YER_BIT		; UC SAYI DA GIRILDI
	MOVF	TMP,0
	MOVWF	S1_3		; GIRILEN SAYIYI BIRINCI SAYININ BIRINCI DIGITINE AL
	BSF	HANGI_SAYI,2	; BU REG IN 4 NOLU BITI 1 YAPILDI YANI 3. DIGIT GIRILDI
	GOTO	YER_BIT
	
IKINCI_S
	BTFSC	HANGI_SAYI,7	; BU REGISTERIN 2-3-4 NOLU BITLERINDEN HANGILERI 1 ISE O BASAMAGA SAYI GIRILMISTIR
	GOTO	IKI_2
	MOVF	TMP,0
	MOVWF	S2_1		; GIRILEN SAYIYI BIRINCI SAYININ BIRINCI DIGITINE AL
	BSF	HANGI_SAYI,7	; BU REG IN 5 NOLU BITI 1 YAPILDI YANI IKINCI SAYININ 1. DIGIT GIRILDI
	GOTO	YER_BIT
	
IKI_2
	BTFSC	HANGI_SAYI,6	; BU REGISTERIN 2-3-4 NOLU BITLERINDEN HANGILERI 1 ISE O BASAMAGA SAYI GIRILMISTIR
	GOTO	IKI_3
	MOVF	TMP,0
	MOVWF	S2_2		; GIRILEN SAYIYI BIRINCI SAYININ IKINCI DIGITINE AL
	BSF	HANGI_SAYI,6	; BU REG IN 6 NOLU BITI 1 YAPILDI YANI IKINCI SAYININ 2. DIGIT GIRILDI
	GOTO	YER_BIT	
IKI_3
	BTFSC	HANGI_SAYI,5	; BU REGISTERIN 2-3-4 NOLU BITLERINDEN HANGILERI 1 ISE O BASAMAGA SAYI GIRILMISTIR
	GOTO	YER_BIT		; UC SAYI DA GIRILDI
	MOVF	TMP,0
	MOVWF	S2_3		; GIRILEN SAYIYI BIRINCI SAYININ BIRINCI DIGITINE AL
	BSF	HANGI_SAYI,5	; BU REG IN 7 NOLU BITI 1 YAPILDI YANI IKINCI SAYININ 3. DIGIT GIRILDI
	GOTO	YER_BIT
	
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
YAZ0
	MOVLW	.0
	MOVWF	TMP		; KAYPAD DONUSUMU ICIN
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CALL	YERLESTIR
	RETURN
YAZ1
	MOVLW	.1
	MOVWF	TMP		    ; ALINAN SAYIYI HANGI DIGITE YAZACAGIMIZ BOLUMDE LAZIM OLACAK
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CALL	YERLESTIR
	RETURN
YAZ2
	MOVLW	.2
	MOVWF	TMP		    ; ALINAN SAYIYI HANGI DIGITE YAZACAGIMIZ BOLUMDE LAZIM OLACAK
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CALL	YERLESTIR
	RETURN
YAZ3
	MOVLW	.3
	MOVWF	TMP
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CALL	YERLESTIR
	RETURN
YAZ4
	MOVLW	.4
	MOVWF	TMP
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CALL	YERLESTIR
	RETURN
YAZ5
	MOVLW	.5
	MOVWF	TMP
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CALL	YERLESTIR
	RETURN
YAZ6
	MOVLW	.6
	MOVWF	TMP
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CALL	YERLESTIR
	RETURN
YAZ7
	MOVLW	.7
	MOVWF	TMP
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CALL	YERLESTIR
	RETURN
YAZ8
	MOVLW	.8
	MOVWF	TMP
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CALL	YERLESTIR
	RETURN
YAZ9
	MOVLW	.9
	MOVWF	TMP		    ; ALINAN SAYIYI HANGI DIGITE YAZACAGIMIZ BOLUMDE LAZIM OLACAK
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CALL	YERLESTIR
	RETURN
YAZC
	CALL	EKRAN_SIL
	RETURN
YAZARTI
	MOVLW	0x2B
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CALL	GECIKME_50MSN
	CLRF	HANGI_SAYI
	BSF	HANGI_SAYI,1	    ; GELEN SAYIYI IKINCI SAYI OLARAK AL
	RETURN
	
YAZEKSI
	BTFSS	HANGI_SAYI,0	    ; BURADA - SAYILARIN ISARETLERI VERILIR. EGER * VE / SECILI DEGILSE ISLEM ISARETLERE GORE YAPILIR
	GOTO	S2E		    ; IKINCI SAYIYI EKSI YAP
	BCF	STATUS,Z
	MOVF	S1_1,0		    ; EGER S1_1,S1_2,S1_3 TOPLAMI SIFIR ISE HIC BIRINCI SAYI GIRILMEMISTIR. O ZAMAN ILK SAYI ISARETI -
	ADDWF	S1_2,0
	ADDWF	S1_3,0
	BTFSS	STATUS,Z
	GOTO	S2E
	CLRF	HANGI_SAYI
	BSF	HANGI_SAYI,0
	BSF	SAYI1_IS,0	    ; BIRINCI SAYI ISARET -
	GOTO	EKSBIT
	
S2E	BCF	STATUS,Z
	MOVF	S2_1,0		    ; EGER S2_1,S2_2,S2_3 TOPLAMI SIFIR ISE HIC BIRINCI SAYI GIRILMEMISTIR. O ZAMAN IKINCI SAYI ISARETI -
	ADDWF	S2_2,0
	ADDWF	S2_3,0
	BTFSS	STATUS,Z
	GOTO	EKSRET		    ; SAYI GIRILMIS, - YAZMADAN CIK
	CLRF	HANGI_SAYI
	BSF	HANGI_SAYI,1	    ; GELEN SAYIYI IKINCI SAYI OLARAK AL
	BSF	SAYI2_IS,0	    ; IKINCI SAYI EKSI ISARETLI
  
EKSBIT	MOVLW	0x2D
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
EKSRET	RETURN
	
YAZCARP
	MOVLW	0x2A
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CLRF	HANGI_SAYI
	BSF	HANGI_SAYI,1	    ; GELEN SAYIYI IKINCI SAYI OLARAK AL
	MOVLW	.2		    ; TOPLAMA-CIKARMA=.1, CARPMA=.2, BOLME=.4
	MOVWF	HANGI_ISL   
	RETURN
	
YAZBOL
	MOVLW	0x2F
	CALL	LCD_KARAKTER_YAZ
	CALL	GECIKME_50MSN	    ; TUS GECIKMESI ICIN YAKLASIK 150 MSN YETERLI
	CALL	GECIKME_50MSN
	CLRF	HANGI_SAYI
	BSF	HANGI_SAYI,1	    ; GELEN SAYIYI IKINCI SAYI OLARAK AL
	MOVLW	.4		    ; TOPLAMA-CIKARMA=.1, CARPMA=.2, BOLME=.4
	MOVWF	HANGI_ISL   
	RETURN	
	

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	

ESITTIR

	MOVLW	0x01
	CALL	KOMUT_8BIT
	CALL	OLUSTUR		    ; KEYPAD DEN GIRILEN SAYILARI BINARY FORMA GETIR (MAKSIMUM 255 GIRILMELIDIR)
	
	BTFSC	HANGI_ISL,2	    ; BOME MI
	GOTO	EBOL
	BTFSC	HANGI_ISL,1	    ; CARPMA MI
	GOTO	ECARP
	GOTO	ETOPCIK

EBOL	CALL	BOL
	GOTO	ESSON
ECARP	CALL	CRP
	GOTO	ESSON
ETOPCIK	CALL	TC
		
ESSON	RETURN
	
	
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
OLUSTUR
	CLRF	SAYI1
	BCF	STATUS,Z
	MOVF	S1_1,1	    ; 3. D?G?T 0 ISE ATLA
	BTFSC	STATUS,Z
	GOTO	OS1S
	BCF	STATUS,Z
	MOVLW	.100
OS1	ADDWF	SAYI1,1
	DECF	S1_1,1
	BTFSS	STATUS,Z
	GOTO	OS1
	
OS1S	
	BCF	STATUS,Z
	MOVF	S1_2,1	    ; 2. D?G?T 0 ISE ATLA
	BTFSC	STATUS,Z
	GOTO	OS2S
	BCF	STATUS,Z
	MOVLW	.10
OS2	ADDWF	SAYI1,1
	DECF	S1_2,1
	BTFSS	STATUS,Z
	GOTO	OS2
	
OS2S
	BCF	STATUS,Z
	MOVF	S1_3,1	    ; 1. D?G?T 0 ISE ATLA
	BTFSC	STATUS,Z
	GOTO	OSDS2
	BCF	STATUS,Z
	MOVLW	.1
OS3	ADDWF	SAYI1,1
	DECF	S1_3,1
	BTFSS	STATUS,Z
	GOTO	OS3

OSDS2
	CLRF	SAYI2	    ; SIMDI SAYI 2
	BCF	STATUS,Z
	MOVF	S2_1,1	    ; 3. D?G?T 0 ISE ATLA
	BTFSC	STATUS,Z
	GOTO	OSD1S
	BCF	STATUS,Z
	MOVLW	.100
OSD1	ADDWF	SAYI2,1
	DECF	S2_1,1
	BTFSS	STATUS,Z
	GOTO	OSD1
	
OSD1S	
	BCF	STATUS,Z
	MOVF	S2_2,1	    ; 2. D?G?T 0 ISE ATLA
	BTFSC	STATUS,Z
	GOTO	OSD2S
	BCF	STATUS,Z
	MOVLW	.10
OSD2	ADDWF	SAYI2,
	DECF	S2_2,1
	BTFSS	STATUS,Z
	GOTO	OSD2
	
OSD2S
	BCF	STATUS,Z
	MOVF	S2_3,1	    ; 1. D?G?T 0 ISE ATLA
	BTFSC	STATUS,Z
	GOTO	RTN
	BCF	STATUS,Z
	MOVLW	.1
OSD3	ADDWF	SAYI2,1
	DECF	S2_3,1
	BTFSS	STATUS,Z
	GOTO	OSD3
	
RTN	RETURN
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; LCD YE YAZ ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
YAZ
	CALL	BCD_KODLA
	MOVF	BCD5,0
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	
	MOVF	BCD4,0
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	
	MOVF	BCD3,0
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	
	MOVF	BCD2,0
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	
	MOVF	BCD1,0
	CALL	ASCII_LOOKUP
	CALL	LCD_KARAKTER_YAZ
	
	MOVLW	.1
	MOVWF	SATIR_KONUM
	MOVLW	.0
	MOVWF	SUTUN_KONUM
	CALL	KURSOR_KONUM
	
	RETURN
;******************************************************************************************************
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; ISARETE GORE TOPLA CIKAR;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
TC
					
	; TOPLAMA MI CIKARMA MI ISARETLERE BAK. SAYI_IS 'IN 0 BITI 0 ISE POSITIF, 1 ISE NEGATIF DIR
	MOVF	SAYI1_IS,0
	ADDWF	SAYI2_IS,1    ; ISARETLER TOPLAMI 0 ISE POZITIF TOPLA, 2 ISE NEGATIF TOPLA, 1 ISE CIKAR
	
	MOVLW	0x00		;POZITIF TOPLAMA
	SUBWF	SAYI2_IS,0
	BTFSC	STATUS,Z
	GOTO	P_TOPLA
	
	MOVLW	0x02		; NEGATIF TOPLAMA
	SUBWF	SAYI2_IS,0
	BTFSC	STATUS,Z
	GOTO	N_TOPLA
	
	MOVLW	0x01		; CIKARMA
	SUBWF	SAYI2_IS,0
	BTFSC	STATUS,Z
	GOTO	CCIKAR
	
CCIKAR
	CALL	CIKAR
	BTFSS	EKSI,0	    ;EGER SONUC EKSI DEGILSE SADECE BCD RAKAMLARI YAZ, EKSI ISE - YAZ VE SONUCUN 2 YE COMPLEMENTINI AL
	GOTO	POSTF
	MOVLW	0x2D	; EKSI ISARETI
	CALL	LCD_KARAKTER_YAZ
	
	COMF	SAYI_L,0    ; SONUC EKSI OLDUGU ICIN 2 YE KOMPLEMENT ALINACAK
	ADDLW	0x01
	MOVWF	SAYI_L
POSTF	
	CALL	YAZ
	RETURN
	
P_TOPLA
	CALL	TOPLA
	CALL	YAZ
	RETURN
	
N_TOPLA
	CALL	TOPLA
	MOVLW	0x2D		    ; SONUCA SADECE EKSI ISARETI EKLE
	CALL	LCD_KARAKTER_YAZ
	CALL	YAZ
	RETURN
; TOPLA-CIKAR BITTI----------------------------------------------------------
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;;;;;;;;;;;;;;;;;;;;;; ÇARP ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
CRP
	CLRF	SAYI_L
	CLRF	SAYI_H
	BCF	STATUS,Z
	
	MOVF	SAYI1,1
	BTFSC	STATUS,Z
	GOTO	CBIT	    ; SAYI1 SONUC 0
	
	MOVF	SAYI2,1
	BTFSC	STATUS,Z    ; SAYI2 0 ISE SONUC SIFIR
	GOTO	CBIT
	
	BCF	STATUS,Z
	
CT	DECF	SAYI2,1	    ; SAYI2 KADAR SAYI1 I KENDISINE EKLE
	BTFSS	STATUS,Z    ;
	GOTO	CT2
	CALL	CARP	    ; SAYI2 1 DEN 0 A DUSUNCE DONGUDEN CIKAR VE SON TOPLAMAYI YAPMAZ. BU NEDENLE SON BIR TOPLAMA DAHA
	MOVF	SAYI1_IS,0  ; CARPMA BITTIKTEN SONRA ISARETI EKLE
	ADDWF	SAYI2_IS,1    ; ISARETLER TOPLAMI 1 ISE EKSI ISARET KOY
	
	BCF	STATUS,Z
	MOVLW	.1
	SUBWF	SAYI2_IS,0
	BTFSS	STATUS,Z
	GOTO	CBIT
	MOVLW	0x2D		    ; SONUCA SADECE EKSI ISARETI EKLE
	CALL	LCD_KARAKTER_YAZ
	

CBIT	CALL	YAZ	    
	RETURN
CT2
	CALL	CARP
	GOTO	CT
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; ÇARP BITTI ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
BOL
	CLRF	SAYI_L
	CLRF	SAYI_H
	BCF	STATUS,Z
	
	MOVF	SAYI1,1
	BTFSC	STATUS,Z
	GOTO	BYAZ	    ; SAYI1 SONUC 0
	
	MOVF	SAYI2,1
	BTFSC	STATUS,Z    ; SAYI2 0 ISE SONUC TANIMSIZ
	GOTO	BEKSI
	
	BCF	STATUS,Z
	BSF	STATUS,C
	
	MOVF	SAYI2,0	    ; BOLUNECEK SAYIYI
BLL	SUBWF	SAYI1,1	    ; SAYI1-SAYI2
	BTFSC	STATUS,C    ;
	GOTO	BLC
	
	MOVF	SAYI1_IS,0  ; BOLME BITTIKTEN SONRA ISARETI EKLE
	ADDWF	SAYI2_IS,1    ; ISARETLER TOPLAMI 1 ISE EKSI ISARET KOY
	BCF	STATUS,Z    
	MOVLW	.1
	SUBWF	SAYI2_IS,0
	BTFSS	STATUS,Z
	GOTO	BYAZ
	MOVLW	0x2D		    ; SONUCA SADECE EKSI ISARETI EKLE
	CALL	LCD_KARAKTER_YAZ
	GOTO	BYAZ

BLC
	INCF	SAYI_L,1   ; HER POZITIF BOLUMDE SONUCU 1 ATTIR
	GOTO	BLL
	
BYAZ	CALL	YAZ
	RETURN
	
BEKSI
	MOVLW	0x2D		    ; TANIMSIZ GOSTERMEK ICIN EKSI KULLAN
	CALL	LCD_KARAKTER_YAZ
	RETURN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; BOL BITTI ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
	
;******************************************************************************************************
BASLA
	BANKSEL	TRISD
	MOVLW	0xF0		; 0-3 ÇIKI? 4-7 G?R??
	MOVWF	TRISD
	
	BANKSEL	TEMIZLE
	CALL	TEMIZLE
	CALL	LCD_AYARLA	; LCD BASLANGIC AYARLA
	CLRF	S1_1		; KEYPAD DEGISKENLERINI TEMIZLE
	CLRF	S1_2
	CLRF	S1_3
	CLRF	S2_1		
	CLRF	S2_2
	CLRF	S2_3
	CLRF	HANGI_SAYI
	BSF	HANGI_SAYI,0	; VARSAYILAN OLARAK GIRILEN ILK SAYI BIRINCI SAYI
	
	BCF	SAYI1_IS,0    ; ILK SAYI ISARETI (0 B?T 1 ?SE NEGATIF)
	BCF	SAYI2_IS,0    ; IKINCI SAYI IS (0 B?T 1 ISE NEGATIF)


	
	
BITIR
	CALL	GECIKME_50MSN	    ; TUS TARAMA GECIKMESI
	CALL	GECIKME_50MSN
	
	MOVLW	b'11111110'	; ILK SUTUNA 0 VER VE RD4-7 DEN HANGISI 0 ISE SIRASIYLA 7-4-1 VEYA = KARAKTERIDIR
	MOVWF	PORTD
	BTFSS	PORTD,4
	CALL	YAZ7
	BTFSS	PORTD,5
	CALL	YAZ4
	BTFSS	PORTD,6
	CALL	YAZ1
	BTFSS	PORTD,7
	CALL	ESITTIR
	
	;CALL	GECIKME_50MSN
	
	MOVLW	b'11111101'	; IKINCI SUTUNA 0 VER VE RD4-7 DEN HANGISI 0 ISE SIRASIYLA 8-5-2 VEYA 0 KARAKTERIDIR
	MOVWF	PORTD
	BTFSS	PORTD,4
	CALL	YAZ8
	BTFSS	PORTD,5
	CALL	YAZ5
	BTFSS	PORTD,6
	CALL	YAZ2
	BTFSS	PORTD,7
	CALL	YAZ0
	
	;CALL	GECIKME_50MSN
	
	MOVLW	b'11111011'	; UCUNCU SUTUNA 0 VER VE RD4-7 DEN HANGISI 0 ISE SIRASIYLA 9-6-3 VEYA C KARAKTERIDIR
	MOVWF	PORTD
	BTFSS	PORTD,4
	CALL	YAZ9
	BTFSS	PORTD,5
	CALL	YAZ6
	BTFSS	PORTD,6
	CALL	YAZ3
	BTFSS	PORTD,7
	CALL	YAZC
	
	
		
	MOVLW	b'11110111'	; DORDUNCU SUTUNA 0 VER VE RD4-7 DEN HANGISI 0 ISE SIRASIYLA + - * /  KARAKTERIDIR
	MOVWF	PORTD
	BTFSS	PORTD,4
	CALL	YAZARTI
	BTFSS	PORTD,5
	CALL	YAZEKSI
	BTFSS	PORTD,6
	CALL	YAZCARP
	BTFSS	PORTD,7
	CALL	YAZBOL
	
	GOTO	BITIR
	END                       ; Program sonu






